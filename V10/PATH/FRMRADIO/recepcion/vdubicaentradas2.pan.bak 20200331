######
# VDUBICAENTRADASCARRO2.PAN 
# 
# Propósito: UBICACION DE CONTENEDORES RECEPCIONADOS
#               
#           
# Autor	   : RFD
# Fecha    : 21-04-2008
####
# Modificaciones:
####
# 
  UBICACION DE CONTENEDOR
  ________ ________________ 
  
 MATRICULA _18_______________
  
 UBI: _20________________ 
      _20________________

  X: volver atras
  M: UBICACION MANUAL
|

PREQUERY=FEJECUTA("CLIMPIAR","ERROR LIMPIANDO DATOS",
                  "CSELCODUBIDEST","ERROR OBTENIENDO\n UBICACION DESTINO",
                  FPOSICIONACAMPO(FSUCCESS,"CODUBI"),"")


POSTQUERY=FEJECUTA( FIF("CSALIR",FEJECUTA("CDELMOV","ERROR AL BORRAR EL MOVIMIENTO",FCOMMIT,"",FPOSICIONABLOQUE("VDUBICAENTRADAS1.PAN"),"")),"ERROR SALIENDO DE PANTALLA",
                    FIF("CMANUAL",FEJECUTA("CDELMOV","ERROR AL BORRAR EL MOVIMIENTO",FCOMMIT,"",FPOSICIONABLOQUE("VDREUBIMANU.PAN"),"ERROR CARGANDO BLOQUE VDREUBIMANU"),FSUCCESS),"ERROR SALIENDO DE PANTALLA",
                   "CSELNOTNULL","ERROR, INFORME UBICACION\n\nPARA VOLVER ATRAS PULSE 'X'",
                   "CVERIFUBI","ERROR\n UBICACION :CODUBI \nINCORRECTA",
                   FEJECUTAMOV("ACTSTKORIGEN",":NCODMOV",":CODRECURSO"),"ERROR EJECUTANDO MOVIMIENTO :NCODMOV",
                   FCOMMIT,"",
                   FPOSICIONABLOQUE("VDUBICAENTRADAS1.PAN"),"ERROR CARGANDO BLOQUE VDUBICAENTRADAS1")
  


CAMPO=CODRECURSO,NOENTER,VIRTUAL
CAMPO=CODOPE,NOENTER,VIRTUAL 
CAMPO=CODMAT,UPPER,VIRTUAL,NOENTER
CAMPO=PINTACODUBIDEST,NOENTER
CAMPO=CODUBIDEST,NOENTER,OCULTO,"_____________"
#CAMPO=CODUBI,SCAN,AUTOENTER,POSTCHANGE=FEJECUTA(FFUERZASCAN("UBI"),"DEBE SCANEAR UBICACION")
CAMPO=CODUBI,SCAN,AUTOENTER
CAMPO=NCODMOV,VIRTUAL,OCULTO
CAMPO=MSGERROR,OCULTO,"_512_"
CAMPO=TAREA,VIRTUAL,OCULTO
CAMPO=MENSAJE,VIRTUAL,OCULTO

CURSOR=CDELMOV DELETE VDMOVIM WHERE CODMOV=:NCODMOV;


CURSOR=CLIMPIAR SELECT '' CODUBI FROM DUAL;

CURSOR=CSELCODUBIDEST SELECT CODUBIDEST,VDCLIPKG.PINTAUBICA(CODUBIDEST) PINTACODUBIDEST FROM VDMOVIM WHERE CODMOV=:NCODMOV;

CURSOR=CSALIR SELECT :CODUBI FROM DUAL WHERE :CODUBI='X';
CURSOR=CMANUAL SELECT :CODUBI FROM DUAL WHERE :CODUBI='M';

CURSOR=CSELNOTNULL SELECT :CODUBI FROM DUAL WHERE :CODUBI IS NOT NULL;

CURSOR=CVERIFUBI SELECT :CODUBI FROM DUAL WHERE SUBSTR(:CODUBI,1,LENGTH(:CODUBI)-1) = SUBSTR(:CODUBIDEST,1,LENGTH(:CODUBIDEST)-1);