# MOdulo   : VDPICKUNICARROART.PAN
# Función  : Formulario de recogida de bultos de unidades con carro.
#            Pantalla de lectura del articulo
#
# Creación : 12-05-2008
# Autor    : JMM
###########################################
# HistOrico de cambios:
# 
PICKING DE CARRO POR TERMINAL
_10_______ _10_______  

UBICACION : ____________

  COGER
ARTICULO: _20_____________
  _20_________________
  _20_________________
LOTE: _20_________________

LEA ARTICULO Y LOTE
_255__________________________________________________________________________________________
|

PREQUERY=FEJECUTA(FIF("-CDAMEDATOS",FPOSICIONABLOQUE("VDNOHAYMOVSPEND.PAN"),FSUCCESS),"\n\nNO HAY \nMOVIMIENTOS\nPENDIENTES",
                  FIF(FCONGELAMOV(":CODMOV"),FSUCCESS,FEJECUTA("CMOVPDTESTOCK","ERROR ACTUALIZANDO MOVTO A PDTESTOCK",
                                                                FCOMMIT,"",
                                                                FPOSICIONABLOQUE("VDPICKUNICARRERRCONGELA.PAN"))),"ERROR\n CONGELANDO MOVIMIENTO\n :CODMOV",
                  "CSELLOTEYCANTIDAD","ERROR\n OBTENIENDO LOTE Y CANTIDAD",
                  FCOMMIT,"ERROR\n HACIENDO COMMIT")               
                   

POSTQUERY=FEJECUTA(FIF("CSELNOTNULL",FPOSICIONABLOQUE("VDPICKUNICARROANUL.PAN"),FSUCCESS),"ERROR\n AL VOLVER AL MENU",
                   "CVERIFART","ERROR\n ARTICULO INCORRECTO",
                   "CVERIFLOTE","ERROR\n LOTE INCORRECTO",
                   FPOSICIONABLOQUE("VDPICKUNICARROBUL.PAN"),"ERROR\n CARGANDO BLOQUE\n VDPICKUNICARROBUL.PAN")

# DEFINICION DE CAMPOS
CAMPO=CODRECURSO,NOENTER,VIRTUAL
CAMPO=CODOPE,NOENTER,VIRTUAL
CAMPO=CODUBIORI,NOENTER
CAMPO=CODART,NOENTER
CAMPO=DESART1,NOENTER
CAMPO=DESART2,NOENTER
CAMPO=CODLOT,NOENTER
CAMPO=CODBARRAS,SCAN,POSTCHANGE=FEJECUTA(FLEEARTICULO("CODBARRAS","CODARTIN",":CODART","CODLOTIN"),"ERROR\n OBTENIENDO ARTICULO Y LOTE")
CAMPO=CODMOV,OCULTO,"@L@@@@@@@"
CAMPO=CANTIDAD,OCULTO,"#L#############",CONVIERTE=FCONVIERTEARTFROMDB(":CODART");FCONVIERTEART2DB(":CODART")
CAMPO=CODARTIN,OCULTO,"_20_",POSTCHANGE=FEJECUTA(FFUERZASCAN("ART"),"\nERROR\n DEBE SCANNEAR\n EL ARTICULO")
CAMPO=CODAREASPICK,OCULTO,"_255_"
CAMPO=CODLOTIN,OCULTO,"_20_"

# DEFINICION DE CURSORES

CURSOR=CDAMEDATOS SELECT MOV.CODMOV,MOV.CODUBIORI, MOV.CODART,  
                         VD.DIVIDEXBUSQUEDA(1,ART.DESART,' ',20,0) DESART1,VD.DIVIDEXBUSQUEDA(2,ART.DESART,' ',20,0) DESART2
                    FROM VDUBICA UBI, VDBULTOLIN BUL, VDMOVIM MOV, VDBULTOCAB BUC, VDARTIC ART
                   WHERE MOV.CODRECURSO = :CODRECURSO 
                     AND MOV.STATUS  = #STMOVASIGNADO 
                     AND MOV.TAREA = 'PEDUNI'
                     AND BUL.CODMOV   = MOV.CODMOV
                     AND UBI.CODUBI  = MOV.CODUBIORI
                     AND ART.CODART   = MOV.CODART
                     AND BUC.CODBULTO = BUL.CODBULTO
                     AND BUC.STATUS = #STBUCPDTESERVIR
                   ORDER BY MOV.PRIOMOV,UBI.ORDENMOVIM;

CURSOR=CSELNOTNULL SELECT :CODBARRAS
                     FROM DUAL 
                    WHERE :CODBARRAS IS NULL;

CURSOR=CSELLOTEYCANTIDAD SELECT CODLOT,CANTIDAD FROM VDMOVIM WHERE CODMOV=:CODMOV;

CURSOR=CVERIFART SELECT :CODARTIN
                   FROM DUAL
                  WHERE :CODARTIN = :CODART;

CURSOR=CVERIFLOTE SELECT :CODLOTIN
                    FROM DUAL
                   WHERE :CODLOTIN = :CODLOT;

CURSOR=CMOVPDTESTOCK UPDATE VDMOVIM 
                        SET CODRECURSO=NULL, STATUS=#STMOVPDTESTOCK,
                            CODOPEMODIF = :CODOPE, FECMODIF = VD.FECHASYS, HORAMODIF = VD.HORASYS
                      WHERE CODMOV=:CODMOV;   