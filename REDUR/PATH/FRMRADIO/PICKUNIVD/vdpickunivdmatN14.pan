# Módulo   : VDPICKUNIVDMAT.PAN
# Función  : Formulario de recogida de bultos de unidades.
#            Pantalla de lectura de matrícula.
#
# Creación : 19-03-2020
# Autor    : FPD
###########################################
# Histórico de cambios:
#
PICKING DE UNIDADES
_10_______ _10_______  
_____________________

LEER MATRICULA  
_255_______________________________

|

PREQUERY=FEJECUTA("+CINITCAMPOS","","CSELNAVE","")

POSTQUERY=FEJECUTA(FIF("CSELNULL",FCARGAFORM(""),FSUCCESS),"\n\n ERROR\n AL VOLVER AL MENU",
                   "CELIMINA00", "ERROR AL TRATAR MATRICULA",
                   "CMATCORRECTA","\n\n LEA MATRICULA CORRECTA\n DEBE TENER :LONMAT DIGITOS",
                   "+CSELORDENUBIREC","\n\n ERROR CONSULTANDO \n DATOS DE RECURSO",
                   "CSELBULTO","\n\n NO HAY BULTOS\n QUE PREPARAR",
                   "-CEXISTEBULTO","\n\n MATRICULA YA UTILIZADA",
                   "CASIGNABULTO","\n\n NO HAY BULTOS\n QUE ASIGNAR",
                   "+CSELPEDBULTO","\n\n NO EXISTE BULTO",
                   "CUPDMOVS","\n\n NO SE PUEDEN ACTUALIZAR\n MOVIMIENTOS DEL BULTO",
                   FCOMMIT,"",
                   FPOSICIONABLOQUE("VDPICKUNIVDPDTE.PAN"),"\n\n ERROR CARGANDO BLOQUE \n VDPICKUNIVDPDTE.PAN")

# DEFINICION DE CAMPOS
CAMPO=CODRECURSO,NOENTER,VIRTUAL
CAMPO=CODOPE,NOENTER,VIRTUAL
CAMPO=TEXTOTAREA,NOENTER
CAMPO=MATCAJA,SCAN,POSTCHANGE=FEJECUTA(FFUERZASCAN("BULTO"),"\n\n DEBE LEER LA MATRICULA"),AUTOENTER
CAMPO=LONMAT,OCULTO,"@L@"
CAMPO=ORDENSALIDA,OCULTO,"@@@@@@@@@"
CAMPO=CODBULTO,OCULTO,"_18"   
CAMPO=CODPED,OCULTO,"_20"
CAMPO=NBULTO,OCULTO,"@L@@@@"

# DEFINICION DE CURSORES
CURSOR=CINITCAMPOS SELECT '' MATCAJA, '' CODBULTO,VD.GETPROP('LETIQMAT') LONMAT,0 ORDENSALIDA,'PICKING DE UNIDADES' TEXTOTAREA FROM DUAL;

CURSOR=CSELNULL SELECT :MATCAJA FROM DUAL WHERE TRIM(:MATCAJA) IS NULL;

CURSOR=CELIMINA00 SELECT CASE WHEN LENGTH(:MATCAJA)=20 AND :MATCAJA LIKE '00%' THEN SUBSTR(:MATCAJA,3) ELSE :MATCAJA END MATCAJA FROM DUAL;

CURSOR=CMATCORRECTA  SELECT TRIM(:MATCAJA) MATCAJA FROM DUAL WHERE LENGTH(TRIM(:MATCAJA))=:LONMAT;

CURSOR=CSELORDENUBIREC SELECT ORDENSALIDA FROM VDUBICA UBI,VDRECURSO REC 
                        WHERE CODRECURSO=:CODRECURSO AND REC.ULTCODUBI=UBI.CODUBI;

CURSOR=CSELBULTO SELECT BUC.CODBULTO
                   FROM VDBULTOCAB BUC,VDBULTOLIN BUL,VDMOVIM MOV,VDUBICA UBI,VDPEDIDOCAB PEC,VDSERIEPREP SEP
                  WHERE BUC.STATUS=VDST.FBUCRESERVADO AND BUL.CODBULTO=BUC.CODBULTO AND BUL.CODMOV=MOV.CODMOV AND 
                        UBI.CODUBI=MOV.CODUBIORI AND BUC.TIPOBULTO='U' AND BUC.TIPOVOL=:TEXTOTAREA AND MOV.STATUS<=VDST.FMOVACTUALIZA AND 
                        (MOV.STATUS=VDST.FMOVPDTERECOGE OR BUC.MATCAJA=:MATCAJA) AND 
                        (BUC.MATCAJA IS NULL OR BUC.MATCAJA=:MATCAJA) AND 
                        NOT EXISTS (SELECT MOV1.CODMOV FROM VDMOVIM MOV1,VDBULTOLIN BUL1 WHERE MOV1.CODMOV=BUL1.CODMOV AND BUL1.CODBULTO=BUC.CODBULTO AND MOV1.STATUS<VDST.FMOVPDTERECOGE) AND
                        PEC.CODPED=BUC.CODPED AND PEC.CODDIV=BUC.CODDIV AND PEC.ANOPED=BUC.ANOPED AND PEC.SEQPED=BUC.SEQPED AND 
                        SEP.CODSERIEPREP=PEC.CODSERIEPREP AND (NVL(PEC.SWTPREPMONO,'N')='N' OR BUC.TIPOVOL='ALTURA')
                   GROUP BY BUC.CODBULTO,BUC.MATCAJA,SEP.PRIORIDAD,PEC.PRIORIDAD 
                   ORDER BY (CASE WHEN :MATCAJA=NVL(BUC.MATCAJA,' ') THEN 0 ELSE 1 END),
                            PEC.PRIORIDAD,SEP.PRIORIDAD, 
                            (SELECT COUNT(*) FROM VDMOVIM MOV,VDBULTOLIN BUL WHERE BUL.CODBULTO=BUC.CODBULTO AND MOV.CODMOV=BUL.CODMOV AND MOV.STATUS BETWEEN VDST.FMOVPDTESTOCK AND VDST.FMOVSINSTOCK), 
                            (CASE WHEN MIN(ORDENSALIDA)>=:ORDENSALIDA THEN MIN(ORDENSALIDA) ELSE 1000000000+MIN(ORDENSALIDA) END);

CURSOR=CEXISTEBULTO SELECT :MATCAJA FROM VDBULTOCAB 
                     WHERE MATCAJA=:MATCAJA AND CODBULTO!=:CODBULTO;

CURSOR=CASIGNABULTO BEGIN                                
                      IF :CODBULTO IS NULL THEN
                         RAISE NO_DATA_FOUND;
                        ELSE
                         UPDATE VDBULTOCAB SET MATCAJA=:MATCAJA WHERE CODBULTO=:CODBULTO;
                         UPDATE VDRECURSO SET CODMAT='' WHERE CODRECURSO=:CODRECURSO;
                         COMMIT;
                       END IF;
                    END;@

CURSOR=CSELPEDBULTO SELECT BUC.CODPED,BUC.NBULTO
                      FROM VDBULTOCAB BUC
                     WHERE BUC.CODBULTO=:CODBULTO;
					
CURSOR=CUPDMOVS UPDATE VDMOVIM
                   SET STATUS=VDST.FMOVASIGNADO,
                       CODRECURSO=:CODRECURSO,
                       CODOPEMODIF=:CODOPE,
                       FECMODIF=VD.FECHASYS,
                       HORAMODIF=VD.HORASYS
                 WHERE CODMOV IN
                       (SELECT BUL.CODMOV FROM VDBULTOLIN BUL,VDMOVIM MOV 
                         WHERE BUL.CODBULTO=:CODBULTO AND BUL.CODMOV=MOV.CODMOV AND
                               MOV.STATUS+0=VDST.FMOVPDTERECOGE AND MOV.TAREA LIKE VD.GETPROP('TAREAPEDUNI')||'%');

                    