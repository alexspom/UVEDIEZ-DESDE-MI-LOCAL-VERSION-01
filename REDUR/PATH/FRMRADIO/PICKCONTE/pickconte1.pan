# MOdulo   : PICKCONTE1.PAN
# Función  : Recogida en origen de un palet completo
#
# Creación : 19-05-2008
# Autor    : JMM
###########################################
# HistOrico de cambios:
#
MOVIMIENTO DE CONTENEDOR
_10_______ _10_______  
PALLET COMPLETO

UBICACION: ________________
MATRICULA: _18_______________
        
ARTICULO: _20_________________
_40____________________________
_40____________________________
   
UBICACION: _20_________________
MATRICULA: _18_______________

|



PREQUERY=FEJECUTA(FIF("CRESTAURAMOVDEST",FEJECUTA(FPOSICIONABLOQUE("PICKCONTE2.PAN"),"\n ERROR \n AL CARGAR PANTALLA"),
                                         FEJECUTA("CSELCODMOV","\n ERROR OBTENIENDO\n DATOS DEL MOVIMIENTO\n :CODMOV")),"",
                  "CSELLONGERROR","\n ERROR\n OBTENIENDO PROPIEDAD",
                  "+CSELMULTIART","",
                  "CSELCONVCANTYUNI","\n ERROR\n AL CONVERTIR\n CANTIDAD Y UNIDADES",
                  "CPONCANTIDADES","\n ERROR\n PONIENDO CANTIDADES",
                  "+CSELAREA","")

POSTQUERY=FEJECUTA(FIF("CSELMATNULL",FEJECUTA(FCARGAFORM(""),"\n ERROR\n VOLVIENDO AL MENU"),
                                     FSUCCESS),"",
                   "CSELVERIFCNT","\n PALLET INCORRECTO\n LEIDO :CODMATORILEIDO\n ESPERADO :CODMATORI",
                   "CVERIFICAUBI","\n UBICACION INCORRECTA", 
                   "CVERERROR",":V10ERROR",                                    
                   "CSELVERIFCNT","\n PALLET INCORRECTO\n LEIDO :CODMATORILEIDO\n ESPERADO :CODMATORI",
				   "CSELIMPRE","\nASEGURESE DE QUE TIENE\n IMPRESORA ASOCIADA",
                   FIF(FRECOGESTKSYNC("ACTSTKORIGEN",":CODMOV",":CODRECURSO"),
                       FSUCCESS,
                       FEJECUTA("+CDIVIDEERROR","\n ERROR\n DIVIDIENDO MENSAJE",FFAILURE,":MSGERROR")),"",                   
				   FIF(FIMPREBULTOSDEMOV(":CODMOV",":CODIMPRE"),
                       FSUCCESS,
                       FEJECUTA("CDIVIDEERROR","ERROR\nDIVIDIENDO ERROR IMPRESION",FFAILURE,":MSGERROR")),"ERROR AL IMPRIMIR ETIQUETA",														 
                   FCOMMIT,"\n ERROR\n HACIENDO COMMIT.",
                   FPOSICIONABLOQUE("PICKCONTE2.PAN"),"\n ERROR \n AL CARGAR PANTALLA")


# DEFINICION DE CAMPOS
CAMPO=CODRECURSO,NOENTER,VIRTUAL
CAMPO=CODOPE,NOENTER,VIRTUAL
CAMPO=TAREA,OCULTO,"_32____________________"
CAMPO=CODMOV,VIRTUAL,OCULTO
CAMPO=PINTAUBI,NOENTER
CAMPO=CODUBIORI,OCULTO,"_20_"
CAMPO=CODMATORI,NOENTER
CAMPO=CODUBIDEST,OCULTO,"_20"
CAMPO=LITNCAJAS,OCULTO,"_____"
CAMPO=LITUNIEMB,OCULTO,"___"
CAMPO=LITUNIDSUELTAS,OCULTO,"________"
CAMPO=NCAJAS,OCULTO,"#####"
CAMPO=UNIEMB,OCULTO,"#L#######",CONVIERTE=FCONVIERTEARTFROMDB(":CODART");FCONVIERTEART2DB(":CODART")
CAMPO=UNIDSUELTAS,OCULTO,"#L#######",CONVIERTE=FCONVIERTEARTFROMDB(":CODART");FCONVIERTEART2DB(":CODART")
CAMPO=LITTOTALUNID,OCULTO,"______________"
CAMPO=TOTALUNID,OCULTO,"#L#############",CONVIERTE=FCONVIERTEARTFROMDB(":CODART");FCONVIERTEART2DB(":CODART")
CAMPO=UNIDADESHOST,OCULTO,"_10__"
CAMPO=LITCODART,OCULTO,"__________"
CAMPO=CODART,NOENTER
CAMPO=DESART1,NOENTER
CAMPO=DESART2,NOENTER
CAMPO=CODUBIORILEIDO,SCAN,POSTCHANGE=FDESIGNACION(FFUERZASCAN("UBI"),"\n DEBE LEER\n LA UBICACION")
CAMPO=CODMATORILEIDO,SCAN,AUTOENTER,POSTCHANGE=FEJECUTA(FFUERZASCAN("CNT"),"\n DEBE LEER\n EL PALLET")
CAMPO=MSGERROR,OCULTO,"_512_"
CAMPO=NUMARTICS,OCULTO,"@L@@@"
CAMPO=CODAREA,OCULTO,"_20_"
CAMPO=LONGERROR,OCULTO,"@@@"
CAMPO=NCAJAS,OCULTO,"@L@@"
CAMPO=TIPO,OCULTO,"_255"
CAMPO=RET,OCULTO,"_200_"
CAMPO=CODIMPRE,OCULTO,"_15_"


# DEFINICION DE CURSORES
CURSOR=CSELIMPRE SELECT CODIMPRE 
                   FROM VDRECURSO 
                  WHERE CODRECURSO=:CODRECURSO AND CODIMPRE IS NOT NULL;
				  
CURSOR=CRESTAURAMOVDEST SELECT MOV.TAREA, MOV.CODMOV, MOV.CODUBIORI, MOV.CODUBIDEST, MOV.CODMATORI
                          FROM VDMOVIM MOV 
                         WHERE STATUS=VDST.FMOVENUBIINTER AND 
                               CODRECURSO=:CODRECURSO  AND
                               CODMATORI = CODMATDEST;

CURSOR=CSELCODMOV SELECT MOV.TAREA, MOV.CODMOV, MOV.CODUBIORI, MOV.CODUBIDEST, MOV.CODMATORI, MOV.CODART,
                         VD.DIVIDEXBUSQUEDA(1,ART.DESART,' ',28,0) DESART1,VD.DIVIDEXBUSQUEDA(2,ART.DESART,' ',28,0) DESART2,
                         ART.DECIMALES UNIEMB__DECIMALES, ART.DECIMALES UNIDSUELTAS__DECIMALES, ART.DECIMALES TOTALUNID__DECIMALES,
                         ART.UNIDADESHOST, MOV.UNIEMB, MOV.CODUBIORI PINTAUBI
                    FROM VDMOVIM MOV, VDARTIC ART
                   WHERE MOV.CODMOV=:CODMOV
                     AND MOV.CODART=ART.CODART;

CURSOR=CSELLONGERROR SELECT VD.GETPROP('LONGITUDERROR') LONGERROR FROM DUAL;

CURSOR=CSELMULTIART SELECT COUNT(DISTINCT CODART) NUMARTICS FROM VDSTOCK WHERE CODMAT = :CODMATORI;

CURSOR=CPONCANTIDADES SELECT (:NCAJAS * :UNIEMB + :UNIDSUELTAS )  TOTALUNID,
                             NULL CODMATORILEIDO
                        FROM DUAL;

CURSOR=CSELCONVCANTYUNI SELECT FLOOR(SUM(STK.CANTIDAD/DECODE(STK.UNIEMB,0,1,STK.UNIEMB))) NCAJAS, 
                               SUM(STK.CANTIDAD)-(FLOOR (SUM(STK.CANTIDAD)/DECODE(STK.UNIEMB,0,1,STK.UNIEMB))*DECODE(STK.UNIEMB,0,1,STK.UNIEMB)) UNIDSUELTAS 
                          FROM VDSTOCK STK
                         WHERE STK.CODMAT = :CODMATORI
                         GROUP BY STK.UNIEMB;

CURSOR=CSELMATNULL SELECT :CODMATORILEIDO
                     FROM DUAL
                    WHERE :CODMATORILEIDO IS NULL;

CURSOR=CSELVERIFCNT SELECT :CODMATORILEIDO CODMATORI FROM DUAL WHERE :CODMATORI=:CODMATORILEIDO; 

CURSOR=CSELUBICONTE SELECT CODUBI CODUBIORILEIDO 
                      FROM VDCONTE 
                     WHERE CODMAT=:CODMATORI;

CURSOR=CSELVERIFUBI SELECT :CODMATORI FROM DUAL 
                   WHERE :CODUBIORILEIDO=:CODUBIORI ; 

CURSOR=CDIVIDEERROR BEGIN :MSGERROR:=VD.DIVERRORRADIO(:V10ERROR,:LONGERROR);
                    END;@

CURSOR=CVERIFICAUBI BEGIN
                   IF :CODUBIORI!=:CODUBIORILEIDO THEN 
                         :V10ERROR:='\n UBICACION INCORRECTA\n LEIDO :CODUBIORILEIDO\n ESPERADO :CODUBIORI';
                     ELSE
                      :V10ERROR:='';
                   END IF;
                 END;@

CURSOR=CVERERROR SELECT :V10ERROR FROM DUAL WHERE :V10ERROR IS NULL;

CURSOR=CVERPARCIAL SELECT :CODUBIORILEIDO 
                   FROM DUAL WHERE RDRUBICA.VERIFICAUBI(:CODUBIORILEIDO, :CODUBIORI) = 1;
				   
CURSOR=CSELAREA SELECT CODAREA FROM VDUBICA WHERE CODUBI=:CODUBIORI;

                 
                                                               
               
                    