# Módulo: VDREUBIMANUCARRO.PAN
# Función: Ubicacion manual de artículo de carro
# Fecha: 16-07-2015
# Autor: CGG
# Histórico de cambios
#
#
  UBICACIÓN DE ARTÍCULO
  ________ ________________ 
  
 ARTIC. _40____________________

 A UBICACION: _20_________________
 
 X: VOLVER 

|

PREQUERY=FEJECUTA("+CSELLIMPIA","")

POSTQUERY=FEJECUTA(FIF("CSALIR",FPOSICIONABLOQUE("VDUBICAENTRADASCARRO2.PAN")),"ERROR SALIENDO DE PANTALLA",
                   "CSELUBI","UBICACION NO EXISTE",
                   "CSELBLOQDEST","\nERROR,\n LA UBICACION DESTINO\n ESTA BLOQUEADA PARA ENTRADA",
                   "CUBICOMPATIBLE","UBICACIÓN INCOMPATIBLE",
                   FIF("CHAYMAT",FEJECUTA("CACTMATDEST","ERROR ACTUALIZANDO\nMATRICULA"),
                                 FEJECUTA("CCREAMATVIRT","",
                                          "CCOMPRUEBAMATTEMP","ERROR CREANDO MATRICULA",
                                          "CACTMATDEST","ERROR ACTUALIZANDO\nMATRICULA")
                      ),"",
                   FEJECUTA("CACTUBIDEST","ERROR ACTUALIZANDO\nUBICACION\nDE MOVIMIENTO"),"",
				           "CUPDMOV","\nERROR ACTUALIZANDO MOVIMIENTO",
                   FCOMMIT,"",
                   FIF("CSELCODUBIDEST",FSUCCESS,
                                        FEJECUTA("CACTCARRO","\nERROR ACTUALIZANDO\nSTATUS DE CONTENEDOR",
                                                 FCOMMIT,"",
                                                 %FFAILURE,"\nNO SE ENCUENTRAN MÁS\nARTÍCULOS QUE UBICAR",
                                                 FPOSICIONABLOQUE("VDUBICAENTRADASCARRO1.PAN"))),"",
                   FPOSICIONABLOQUE("VDUBICAENTRADASCARRO2.PAN"),"ERROR CARGANDO BLOQUE 1")
  


CAMPO=CODRECURSO,NOENTER,VIRTUAL
CAMPO=CODOPE,NOENTER,VIRTUAL 
CAMPO=CODARTDEST,VIRTUAL,NOENTER
CAMPO=CODUBI,SCAN,AUTOENTER,POSTCHANGE=FEJECUTA(FIF("CSELNOTNULLUBI",FIF("CSALIR",FSUCCESS,FFUERZASCAN("UBI"))),"DEBE SCANEAR UBICACION")
CAMPO=CODLOT,VIRTUAL,OCULTO
CAMPO=CODMOV,VIRTUAL,OCULTO
CAMPO=STATUS,OCULTO,"@L@@@@"
CAMPO=CODLOTDEST,VIRTUAL,OCULTO
CAMPO=CODUBIDEST,VIRTUAL,OCULTO
CAMPO=CODMAT,VIRTUAL,OCULTO
CAMPO=NCODMAT,AUXILIAR,OCULTO,"_18"
CAMPO=DUMMY,OCULTO,"_"

CURSOR=CSALIR SELECT :CODUBI FROM DUAL WHERE :CODUBI='X';

CURSOR=CSELLIMPIA SELECT '' CODUBI FROM DUAL;

CURSOR=CSELUBI SELECT :CODUBI FROM VDUBICA WHERE CODUBI=:CODUBI;

CURSOR=CSELBLOQDEST SELECT :CODUBI
                    FROM VDUBICA 
                    WHERE CODUBI = :CODUBI
                    AND BLOQUEOSE = 'N';

CURSOR=CUBICOMPATIBLE SELECT 'X' DUMMY FROM DUAL WHERE RDRUBICA.ESUBICOMPATIBLE(:CODUBI,:CODARTDEST,:CODLOT) = 'S';

CURSOR=CSELCODUBIDEST SELECT A.CODUBIDEST, A.CODART CODARTDEST, A.CODMOV, A.CODLOT CODLOTDEST
                      FROM VDMOVIM A, VDUBICA B
                      WHERE A.CODUBIDEST = B.CODUBI
                      AND A.CODMATORI = :CODMAT
                      AND A.STATUS = 2000
                      AND A.CODCONCE = 'RECEP'
                      AND A.CODRECURSO = :CODRECURSO
                      ORDER BY B.ORDENENTRADA;
                      
CURSOR=CHAYMAT SELECT CODMAT NCODMAT
               FROM VDCONTE
               WHERE CODUBI = :CODUBI
               AND ROWNUM = 1;
               
CURSOR=CACTMATDEST UPDATE VDMOVIM
                   SET CODMATDEST = :NCODMAT
                   WHERE CODMOV = :CODMOV;
                   
CURSOR=CCREAMATVIRT BEGIN
                     :NCODMAT := RDRUBICA.CREAMATVIRT(:CODUBI);
                    END;@
                    
CURSOR=CCOMPRUEBAMATTEMP SELECT 'X' DUMMY FROM DUAL WHERE :NCODMAT <> '0';

CURSOR=CACTUBIDEST UPDATE VDMOVIM
                   SET CODUBIDEST = :CODUBI
                   WHERE CODMOV = :CODMOV;
                   
CURSOR=CACTCARRO UPDATE VDCONTE SET STATUS = 0 WHERE CODMAT=:CODMAT;

CURSOR=CUPDMOV UPDATE VDMOVIM SET STATUS=2300 WHERE CODMOV=:CODMOV;

CURSOR=CSELNOTNULLUBI SELECT 'X' DUMMY FROM DUAL WHERE :CODUBI IS NOT NULL;
